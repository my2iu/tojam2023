 class @ { epilogue generator } extends @ {object } {
 vardecls {
 var . {items } @ {array }
 }
 function . {generate } { @ {string } } { } {
 // Generate the epilogue text
 var . {epilogue } @ {string } :=""
 . {epilogue } := . {epilogue } +"The adventurer takes your gear and heads off for adventure. "

 var . {game over } @ {boolean } := false
 var . {hp } @ {number } := 2

 // Encounter the first foe
 . {epilogue } := . {epilogue } +"They come across a horrendous ogre. "

 if { this . {is item packed: {"golden sword" } } } {
 . {epilogue } := . {epilogue } +"They draw the golden sword, but it is an old antique and made of gold, so it quickly falls apart. "
 this . {remove item: {"golden sword" } }
 }

 if { this . {is item packed: {"sword" } } } {
 . {epilogue } := . {epilogue } +"Using the steel sword, they drive the ogre away. "
 if { this . {is item packed: {"shield" } } } {
 . {epilogue } := . {epilogue } +"They were able to block most of the ogre’s attacks with the shield, so they were only lightly wounded during the battle. "
 . {hp } := . {hp } - 1
 }
 else {
 . {epilogue } := . {epilogue } +"Nonetheless, they were heavily wounded during the ogre’s attacks. "
 . {hp } := . {hp } - 2
 }


 }
 else {
 . {epilogue } := . {epilogue } +"Without a weapon, they must retreat. "

 . {game over } := true
 }

 if { . {game over } . {not } and . {hp } < 2 and this . {is item packed: {"health potion" } } } {
 . {hp } := . {hp } + 1
 if { . {hp } = 2 } {
 . {epilogue } := . {epilogue } +"They are able to use the health potion to restore themselves to full health. "
 }
 else {
 . {epilogue } := . {epilogue } +"They are able to use the health potion to heal some of their wounds, but they are still weak. "
 }

 }

 if { . {game over } . {not } and . {hp } <= 0 } {
 . {epilogue } := . {epilogue } +"Weakened from the battle, the adventurer decides to end their quest. "
 . {game over } := true
 }

 if { . {game over } . {not } } {
 . {epilogue } := . {epilogue } +"The adventurer crosses the murky forest to distant lands. "
 if { this . {is item packed: {"food" } } } {
 . {epilogue } := . {epilogue } +"With nothing to eat in those cursed woods, they need to eat their rations to survive. "
 this . {remove item: {"food" } }
 }
 else {
 . {epilogue } := . {epilogue } +"With nothing to eat in those cursed woods, they become weak from hunger. "
 . {hp } := . {hp } - 1
 }

 if { . {hp } <= 0 } {
 . {epilogue } := . {epilogue } +"Being too weak to continue, the adventurer has to end their quest. "
 . {game over } := true
 }

 }

 if { . {game over } . {not } } {
 . {epilogue } := . {epilogue } +"After traversing the murky woods, they arrive at the sacred temple, where they learn the secret of getting goats to stand on poles. "
 }




 // Is there enough room leftover to bring loot home?




 . {epilogue } := . {epilogue } +"The end. "

 return . {epilogue }
 }
 function . {is item packed: { . {item name } @ {string } } } { @ {boolean } } { } {
 var . {n } @ {number } := 0
 while { . {n } < . {items } . {size } } {
 var . {item } @ {backpack item } := . {items } . {at: { . {n } } } as @ {backpack item }
 if { . {item } . {is placed } } {
 if { . {item } . {name } = . {item name } } {
 return true
 }

 }

 . {n } := . {n } + 1
 }

 return false
 }
 function . {remove item: { . {item name } @ {string } } } { @ {void } } { } {
 var . {n } @ {number } := 0
 while { . {n } < . {items } . {size } } {
 var . {item } @ {backpack item } := . {items } . {at: { . {n } } } as @ {backpack item }
 if { . {item } . {is placed } } {
 if { . {item } . {name } = . {item name } } {
 . {item } . {unplace }
 return this
 }

 }

 . {n } := . {n } + 1
 }

 return this
 }
 constructor . {with items: { . {array } @ {array } } } { } {
 super . {new }
 . {items } := . {array }
 }
 }
