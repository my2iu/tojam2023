 class @ { play screen } extends @ {game screen } {
 vardecls {
 var . {background image } @ {image }
 var . {grid image } @ {image }
 var . {items } @ {array }
 var . {pointer x } @ {number }
 var . {pointer y } @ {number }
 var . {dragging item } @ {backpack item }
 var . {current item index } @ {number }
 var . {grid corner x } @ {number }
 var . {grid corner y } @ {number }
 var . {grid width } @ {number }
 var . {grid height } @ {number }
 var . {grid size } @ {number }
 }
 function . {draw items in suitcase } { @ {void } } { } {
 var . {n } @ {number } := 0
 while { . {n } < . {items } . {size } } {
 var . {item } @ {backpack item } := . {items } . {at: { . {n } } } as @ {backpack item }
 if { . {item } . {is placed } } {
 var . {item image } @ {image } := . {item } . {image }
 . {ctx } . {draw image: { . {item image } }x: { . {item } . {x } * . {grid size } + . {grid corner x } }y: { . {item } . {y } * . {grid size } + . {grid corner y } } }
 }

 . {n } := . {n } + 1
 }

 }
 function . {is in grid pointer x: { . {x } @ {number } }y: { . {y } @ {number } } } { @ {boolean } } { } {
 return . {x } > . {grid corner x } and . {x } < . {grid corner x } + . {grid width } * . {grid size } and . {y } > . {grid corner y } and . {y } < . {grid corner y } + . {grid size } * . {grid height }
 }
 function . {pointer down at x: { . {x } @ {number } }y: { . {y } @ {number } } } { @ {void } } { } {
 . {pointer x } := . {x }
 . {pointer y } := . {y }
 if { . {x } > 100 and . {x } < 380 and . {y } > 300 and . {y } < 500 } {
 . {dragging item } := . {items } . {at: { . {current item index } } } as @ {backpack item }

 }
 if { . {x } > 405 and . {y } > 300 and . {y } < 600 } {
 . {current item index } := . {current item index } + 1
 if { . {current item index } >= . {items } . {size } } {
 . {current item index } := 0
 }

 }
 if { . {x } < 75 and . {y } > 300 and . {y } < 600 } {
 . {current item index } := . {current item index } - 1
 if { . {current item index } < 0 } {
 . {current item index } := . {items } . {size } - 1
 }

 }

 this . {redraw }
 }
 function . {pointer move at x: { . {x } @ {number } }y: { . {y } @ {number } }is down: { . {is down } @ {boolean } } } { @ {void } } { } {
 . {pointer x } := . {x }
 . {pointer y } := . {y }

 this . {redraw }
 }
 function . {pointer up at x: { . {x } @ {number } }y: { . {y } @ {number } } } { @ {void } } { } {
 . {pointer x } := . {x }
 . {pointer y } := . {y }

 if { . {dragging item } != null and this . {is in grid pointer x: { . {pointer x } }y: { . {pointer y } } } } {
 var . {snap x } @ {number } := this . {pointer x to grid x: { . {pointer x } } }
 var . {snap y } @ {number } := this . {pointer y to grid y: { . {pointer y } } }
 . {dragging item } . {place at x: { . {snap x } }y: { . {snap y } } }
 }



 . {dragging item } := null

 this . {redraw }
 }
 function . {pointer x to grid x: { . {x } @ {number } } } { @ {number } } { } {
 return ( ( . {x } - . {dragging item } . {grid width } / 2 * . {grid size } - . {grid corner x } ) / . {grid size } ) . {floor }
 }
 function . {pointer y to grid y: { . {y } @ {number } } } { @ {number } } { } {
 return ( ( . {y } - . {dragging item } . {grid height } / 2 * . {grid size } - . {grid corner y } ) / . {grid size } ) . {floor }
 }
 function . {redraw } { @ {void } } { } {
 . {ctx } . {clear rectangle at x: { 0 }y: { 0 }width: { . {canvas } . {width } }height: { . {canvas } . {height } } }
 . {ctx } . {draw image: { . {background image } }x: { 0 }y: { 0 } }

 var . {image } @ {image }

 // Draw item being looked at

 if { . {current item index } < . {items } . {size } } {
 var . {current backpack item } @ {backpack item } := . {items } . {at: { . {current item index } } } as @ {backpack item }
 if { . {current backpack item } != . {dragging item } } {
 . {image } := . {current backpack item } . {image }
 . {ctx } . {draw image: { . {image } }x: { 240 - . {image } . {width } / 2 }y: { 300 } }
 }

 }

 // Draw items in suitcase
 this . {draw items in suitcase }



 . {ctx } . {draw image: { . {grid image } }x: { 0 }y: { 0 } }


 if { . {dragging item } != null } {

 . {image } := . {dragging item } . {image }
 if { this . {is in grid pointer x: { . {pointer x } }y: { . {pointer y } } } } {
 var . {snap x } @ {number } := this . {pointer x to grid x: { . {pointer x } } }
 var . {snap y } @ {number } := this . {pointer y to grid y: { . {pointer y } } }
 . {ctx } . {draw image: { . {image } }x: { . {grid size } * . {snap x } + . {grid corner x } }y: { . {grid size } * . {snap y } + . {grid corner y } } }
 }
 else {
 . {ctx } . {draw image: { . {image } }x: { . {pointer x } - . {image } . {width } / 2 }y: { . {pointer y } - . {image } . {height } / 2 } }

 }



 }

 }
 function . {run } { @ {void } } { } {
 this . {redraw }
 }
 function . {start } { @ {void } } { } {
 . {ctx } . {draw image: { . {loading image } }x: { 0 }y: { 0 } }

 . {background image } := @ {image } . {from: {"Suitcase.png" } }
 . {grid image } := @ {image } . {from: {"Grid.png" } }
 . {start wait list } . {+=: { . {background image } . {load promise } } } . {+=: { . {grid image } . {load promise } } }

 var . {n } @ {number } := 0
 while { . {n } < . {items } . {size } } {
 var . {item image } @ {image } := . {items } . {at: { . {n } } } as @ {backpack item } . {image }
 if { . {item image } != null } {
 . {start wait list } . {+=: { . {item image } } }
 }
 . {n } := . {n } + 1
 }

 }
 constructor . {new } { } {
 super . {new }
 . {pointer x } := 0
 . {pointer y } := 0
 . {current item index } := 0
 . {grid corner x } := 120
 . {grid corner y } := 80
 . {grid width } := 10
 . {grid height } := 7
 . {grid size } := 24
 . {items } := @ {array } . {with: { @ {backpack item } . {create } . {name: {"sword" } } . {image: { @ {image } . {from: {"Sword.png" } } } } } } . {+=: { @ {backpack item } . {create } . {name: {"shield" } } . {image: { @ {image } . {from: {"Shield.png" } } } } } }

 }
 }
